/*
~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
    PRINT PARAMS SUMMARY
~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
*/

include { paramsSummaryLog; paramsSummaryMap } from 'plugin/nf-schema'

def logo = NfcoreTemplate.logo(workflow, params.monochrome_logs)
def citation = '\n' + WorkflowMain.citation(workflow) + '\n'
def summary_params = paramsSummaryMap(workflow)

// Print parameter summary log to screen
log.info logo + paramsSummaryLog(workflow) + citation

WorkflowSnvs.initialise(params, log)



/*
~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
    VALIDATE INPUTS
~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
*/

// Check mandatory parameters

ch_fasta   = params.fasta ? Channel.fromPath(params.fasta).map{ it -> [ [id:it.baseName], it ] }.collect() : Channel.empty() 
ch_fai     = params.fai ? Channel.fromPath(params.fai).map{ it -> [ [id:it.baseName], it ] }.collect() : Channel.empty()
//ch_snps    = params.known_snps ? Channel.fromPath(params.known_snps).collect() : Channel.value([])
ch_snps = params.known_snps ? 
            Channel.fromPath(params.known_snps.split(',').collect { it.trim() }, checkIfExists: true)
                .collect() : 
            Channel.value([])
//ch_snps_tbi = params.known_snps_tbi ? Channel.fromPath(params.known_snps_tbi).collect() : Channel.empty()
ch_snps_tbi = params.known_snps_tbi ? 
            Channel.fromPath(params.known_snps_tbi.split(',').collect { it.trim() }, checkIfExists: true)
                .collect() : 
            Channel.value([])

ch_variant_catalog = params.variant_catalog ? Channel.fromPath(params.variant_catalog, checkIfExists: true).map{ it -> [ [id:it.baseName], it ] }.collect() : Channel.value([])


//ch_assembly = params.assembly ? Channel.value(params.assembly) : ch_fasta.map { meta, fasta -> meta.id }.first() 
// lo había puesto así porque solo era para poner el id al nombre final del vcf (si no estaba, ponia el nombre del fasta), pero ahora lo he cambiado porque también lo vamos a usar para el VEP
ch_assembly = Channel.value(params.assembly)

//deep variant parameters
ch_gzi = Channel.of([[],[]]).first()
ch_par_bed = params.ch_par_bed ? Channel.fromPath(params.ch_par_bed, checkIfExists: true).map { file -> [ [:], file ] }.collect() : Channel.of([[:], []]).first()


/*
~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
    CONFIG FILES
~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
*/

ch_multiqc_config          = Channel.fromPath("$projectDir/assets/multiqc_config.yml", checkIfExists: true)
ch_multiqc_custom_config   = params.multiqc_config ? Channel.fromPath( params.multiqc_config, checkIfExists: true ) : Channel.empty()
ch_multiqc_logo            = params.multiqc_logo   ? Channel.fromPath( params.multiqc_logo, checkIfExists: true ) : Channel.empty()
ch_multiqc_custom_methods_description = params.multiqc_methods_description ? file(params.multiqc_methods_description, checkIfExists: true) : file("$projectDir/assets/methods_description_template.yml", checkIfExists: true)

/*
~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
    IMPORT LOCAL MODULES/SUBWORKFLOWS
~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
*/

//
// SUBWORKFLOW: Consisting of a mix of local and nf-core/modules
//
include { INPUT_CHECK } from '../subworkflows/local/input_check'
include { MAPPING } from '../subworkflows/local/mapping'
include { GATK_VCF } from '../subworkflows/local/gatk_vcf'
include { DRAGEN_VCF } from '../subworkflows/local/dragen_vcf'
include { VCF_MERGE_VARIANTCALLERS } from '../subworkflows/local/vcf_merge_variantcallers'
include { DEEP_VARIANT_VCF           } from '../subworkflows/local/deep_variant_vcf'
include { SNV_ANNOTATION } from '../subworkflows/local/snv_annotation'
include { SV_CALLING } from '../subworkflows/local/sv_calling'
include { SV_CALLING_DELLY } from '../subworkflows/local/sv_calling_delly'
include { GATK_TRIO_VCF } from '../subworkflows/local/gatk_trio_vcf'
include { CNVS_CALLING } from '../subworkflows/local/cnvs_calling'

/*
~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
    IMPORT NF-CORE MODULES/SUBWORKFLOWS
~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
*/

//
// MODULE: Installed directly from nf-core/modules
//
include { FASTQC                      } from '../modules/nf-core/fastqc/main'
include { MULTIQC                     } from '../modules/nf-core/multiqc/main'
include { CUSTOM_DUMPSOFTWAREVERSIONS } from '../modules/nf-core/custom/dumpsoftwareversions/main'

include { BWA_INDEX } from '../modules/nf-core/bwa/index/main'
include { PICARD_CREATESEQUENCEDICTIONARY } from '../modules/nf-core/picard/createsequencedictionary/main'
include { GATK4_COMPOSESTRTABLEFILE } from '../modules/nf-core/gatk4/composestrtablefile/main'
include { GATK4_CALIBRATEDRAGSTRMODEL } from '../modules/nf-core/gatk4/calibratedragstrmodel/main'
include { ENSEMBLVEP_DOWNLOAD } from '../modules/nf-core/ensemblvep/download/main'

include { ANNOTSV_INSTALLANNOTATIONS } from '../modules/nf-core/annotsv/installannotations/main'

include { GLOWGENES } from '../modules/local/glowgenes/main'

include { EXPANSIONHUNTER } from '../modules/nf-core/expansionhunter/main'

include { MOSDEPTH } from '../modules/nf-core/mosdepth/main'

include { DECOMPRESS_MOSDEPTH_QUANTIZED } from '../modules/local/decompress_mostdepth_quantized/main'


/*
~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
    RUN MAIN WORKFLOW
~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
*/

// Info required for completion email and summary
def multiqc_report = []

workflow SNVS {

    ch_versions = Channel.empty()

    //
    // SUBWORKFLOW: Read in samplesheet, validate and stage input files
    //
    INPUT_CHECK (
        file(params.input)
    )
    ch_versions = ch_versions.mix(INPUT_CHECK.out.versions)
    // TODO: OPTIONAL, you can use nf-validation plugin to create an input channel from the samplesheet with Channel.fromSamplesheet("input")
    // See the documentation https://nextflow-io.github.io/nf-validation/samplesheets/fromSamplesheet/
    // ! There is currently no tooling to help you write a sample sheet schema

    if (params.mapping) {
        if (params.index) { 
            ch_index = Channel.fromPath(params.index).map{ it -> [ [id:it.baseName], it ] }.collect()
        } else { 
            BWA_INDEX (ch_fasta)
            ch_index = BWA_INDEX.out.index
            ch_versions = ch_versions.mix(BWA_INDEX.out.versions)
        }
    }

    if (params.mapping || params.variant_calling) {
        if (params.refdict) { 
            ch_refdict = Channel.fromPath(params.refdict).map{ it -> [ [id:it.baseName], it ] }.collect()
        } else { 
            PICARD_CREATESEQUENCEDICTIONARY (ch_fasta)
            ch_refdict = PICARD_CREATESEQUENCEDICTIONARY.out.reference_dict
            ch_versions = ch_versions.mix(PICARD_CREATESEQUENCEDICTIONARY.out.versions)
        }
    }

    if (params.variant_calling && params.run_dragen) {
        if (params.reference_str) { 
            ch_ref_str = Channel.fromPath(params.reference_str).collect()
        } else { 
            GATK4_COMPOSESTRTABLEFILE (
                ch_fasta.map {meta, fasta -> [fasta] },
                ch_fai.map {meta, fai -> [fai]  },
                ch_refdict.map {meta, dict -> [dict] }
            )
            ch_ref_str = GATK4_COMPOSESTRTABLEFILE.out.str_table
            ch_versions = ch_versions.mix(GATK4_COMPOSESTRTABLEFILE.out.versions)
        }
    }

    ch_gene_list = params.gene_list ? Channel.fromPath(params.gene_list, checkIfExists: true).collect() : Channel.value([])

    // Create ch_intervals channel for snvs detection if targeted_snvs_detection is enabled and intervals file is provided
    if (params.targeted_snvs_detection && params.intervals){
        ch_intervals = INPUT_CHECK.out.reads.map{ meta, fastqs -> tuple(meta, file(params.intervals)) }
    } else if (params.targeted_snvs_detection && !params.intervals) {
        log.error "Targeted SNVs detection is enabled, but no intervals file is provided. Please provide an intervals file using the --intervals parameter."
        System.exit(1)
    } else {
        ch_intervals = INPUT_CHECK.out.reads.map{ meta, fastqs -> tuple(meta, []) }
    }
    
    if (params.glowgenes) {
        if (params.glowgenes_ranking) {
            ch_glowgenes_ranking = Channel.fromPath(params.glowgenes_ranking, checkIfExists: true).collect()
        } else if (params.gene_list) {
                GLOWGENES (
                    ch_gene_list
                )
                ch_glowgenes_ranking = GLOWGENES.out.glow_ranking
        } else { 
            println "No valid glowgenes input provided."  
            ch_glowgenes_ranking = Channel.value([]) 
            }
    } else { ch_glowgenes_ranking = Channel.value([]) }

    if (params.mapping) {
        fastqs = INPUT_CHECK.out.reads.map{meta, reads -> check_fastq(meta, reads)}
        
        // Ensure intervals are created for all samples
        // ch_intervals = params.intervals ? 
        // INPUT_CHECK.out.reads.map{ meta, fastqs -> tuple(meta, file(params.intervals)) } : 
        // INPUT_CHECK.out.reads.map{ meta, fastqs -> tuple(meta, []) }

        // 
        // MODULE: Run FastQC
        //
        FASTQC (
            fastqs
        )
        ch_versions = ch_versions.mix(FASTQC.out.versions.first())

        MAPPING (
            fastqs,
            ch_intervals,
            ch_index,
            ch_fasta,
            ch_fai,
            ch_refdict,
            ch_snps,
            ch_snps_tbi
        )

        ch_versions = ch_versions.mix(MAPPING.out.versions)

        } 
    

    if (params.variant_calling) {
        if (params.mapping) {
            bam_file = MAPPING.out.bam
        } else {
            bam_file = INPUT_CHECK.out.bams.map{ meta, bam, bai -> check_bam(meta, bam, bai) }

            // ch_intervals = params.intervals ? 
            // INPUT_CHECK.out.bams.map{ meta, bam, bai -> tuple(meta, file(params.intervals)) } : 
            // INPUT_CHECK.out.bams.map{ meta, bam, bai -> tuple(meta, []) }

        }

    
        if (params.trio_analysis) {

            ch_intervals_genomicsdbimport = params.genomicsdbimport_interval ? Channel.fromPath(params.genomicsdbimport_interval).collect() : Channel.of([])

            ch_ped = INPUT_CHECK.out.ped.unique()
            GATK_TRIO_VCF (
                bam_file,
                ch_intervals,
                ch_fasta,
                ch_fai,
                ch_refdict,
                ch_snps.map{ it -> [ [id:it.baseName], it ] }.collect(),
                ch_snps_tbi.map{ it -> [ [id:it.baseName], it ] }.collect(),
                ch_intervals_genomicsdbimport, 
                ch_ped // ch_ped            
            )
            ch_versions = ch_versions.mix(GATK_TRIO_VCF.out.versions)

            final_vcf_file = GATK_TRIO_VCF.out.vcf

        } else { 
            
            if (params.run_gatk) {
            GATK_VCF (
                bam_file,
                ch_intervals,
                ch_fasta,
                ch_fai,
                ch_refdict,
                Channel.fromList([tuple([ id: 'dbsnp'],[])]).collect(),
                Channel.fromList([tuple([ id: 'dbsnp_tbi'],[])]).collect()
            )   
            ch_versions = ch_versions.mix(GATK_VCF.out.versions)
            }
            
            if (params.run_deepvariant) {
            DEEP_VARIANT_VCF (
                bam_file,
                ch_intervals,
                ch_fasta,
                ch_fai,
                ch_gzi,
                ch_par_bed
            )
            ch_versions = ch_versions.mix(DEEP_VARIANT_VCF.out.versions)
            }

            if (params.run_dragen) {
            DRAGEN_VCF (
                bam_file, 
                ch_fasta,
                ch_fai,
                ch_refdict,
                ch_ref_str,
                ch_intervals,
                Channel.fromList([tuple([ id: 'dbsnp'],[])]).collect(),
                Channel.fromList([tuple([ id: 'dbsnp_tbi'],[])]).collect()
            )
            ch_versions = ch_versions.mix(DRAGEN_VCF.out.versions)

            }

            ch_gatk = params.run_gatk ? GATK_VCF.out.vcf : bam_file.map{ meta, bam, bai -> tuple(meta, []) }
            ch_dragstr = params.run_dragen ? DRAGEN_VCF.out.vcf : bam_file.map{ meta, bam, bai -> tuple(meta, []) }
            ch_deepvariant = params.run_deepvariant ? DEEP_VARIANT_VCF.out.vcf : bam_file.map{ meta, bam, bai -> tuple(meta, []) }


            // Join the three channels and filter out empty lists while keeping tuple structure
            ch_vcfs_for_merge = ch_gatk
                .join(ch_dragstr)
                .join(ch_deepvariant)
                .map { it ->
                    def meta = it[0]
                    // Get all items after meta, but DON'T flatten - keep them as separate elements
                    def items = it[1..-1]
                    // Filter out empty lists but keep the structure flat
                    def filtered = items.findAll { item -> item != null && item != [] && item.toString() != '[]' }
                    
                    // Return as a flat tuple: [meta, item1, item2, item3, ...]
                    [meta, *filtered]
                }


            VCF_MERGE_VARIANTCALLERS (
                ch_vcfs_for_merge,   
                ch_fasta,
                ch_fai,
                ch_intervals,
                ch_assembly
            )
            ch_versions = ch_versions.mix(VCF_MERGE_VARIANTCALLERS.out.versions)
            final_vcf_file = VCF_MERGE_VARIANTCALLERS.out.vcf

        } 
    
    }
    

    if (params.annotation) {
        if (params.variant_calling) {
            vcf_file = final_vcf_file
        } else {
            vcf_file = INPUT_CHECK.out.vcfs.map{ meta, vcf, tbi -> check_vcf(meta, vcf, tbi) }
            
            // ch_intervals = params.intervals ? 
            // INPUT_CHECK.out.vcfs.map{ meta, vcf, tbi -> tuple(meta, file(params.intervals)) } : 
            // INPUT_CHECK.out.vcfs.map{ meta, vcf, tbi -> tuple(meta, []) }
        }

        ch_custom_extra_files = params.custom_extra_files ? vcf_file.map{ meta, vcf, tbi -> tuple(meta, file(params.custom_extra_files)) } : vcf_file.map{ meta, vcf, tbi -> tuple(meta, []) }

        
        ch_extra_files = params.extra_files ? 
            Channel.fromPath(params.extra_files.split(',').collect { it.trim() }, checkIfExists: true)
                .collect() : 
            Channel.value([])

        // Conditionally add files using mix
        if (params.plugins_dir) {
            ch_extra_files = ch_extra_files.mix(Channel.fromPath("${params.plugins_dir}", checkIfExists: true)).collect()
        }


        ch_extra_files_pvm = params.extra_files_pvm ? 
            Channel.fromPath(params.extra_files_pvm.split(',').collect { it.trim() }, checkIfExists: true)
                .collect() : 
            Channel.value([])

        //ch_glowgenes_ranking = params.glowgenes_ranking ? Channel.fromPath(params.glowgenes_ranking, checkIfExists: true).collect() : Channel.value([])
        ch_glowgenes_sgds = params.sgds ? Channel.fromPath(params.glowgenes_sgds, checkIfExists: true).collect() : Channel.value([])

        if (params.vep_cache_path) { ch_vep_cache_path = Channel.fromPath(params.vep_cache_path, checkIfExists: true).collect() } else { 
            // Define your meta_vep
            def meta_vep = [id: "vep_${params.assembly}", assembly: params.assembly]
            if (params.refseq_cache) {
                ch_vep_download = Channel.of([meta_vep, params.assembly, "${params.species}_refseq", params.vep_cache_version])
            } else {
                ch_vep_download = Channel.of([meta_vep, params.assembly, params.species, params.vep_cache_version])
            }
            ENSEMBLVEP_DOWNLOAD (
                ch_vep_download
                )
            ch_versions = ch_versions.mix(ENSEMBLVEP_DOWNLOAD.out.versions)
            ch_vep_cache_path = ENSEMBLVEP_DOWNLOAD.out.cache.map{ meta, cache -> [cache] }.collect()
        }

        ch_vep_cache_version = params.vep_cache_version ? Channel.value(params.vep_cache_version) : Channel.value([])

        SNV_ANNOTATION (
            vcf_file,
            ch_fasta,
            ch_assembly,
            params.species,
            ch_vep_cache_version,
            ch_vep_cache_path,
            ch_custom_extra_files,
            ch_extra_files,
            params.pvm_script,
            params.maf,
            ch_glowgenes_ranking,
            ch_glowgenes_sgds,
            ch_gene_list,
            ch_extra_files_pvm
        )
        ch_versions = ch_versions.mix(SNV_ANNOTATION.out.versions)
    }

    // ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
    //  STRUCTURAL VARIANTS / CNVs
    // ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~

    if (params.svs) {

        //
        // Common: Get BAM files
        //
        if (params.mapping) {
            ch_bam_svs = MAPPING.out.bam
        } else {
            ch_bam_svs = INPUT_CHECK.out.bams.map{ meta, bam, bai -> check_bam(meta, bam, bai) }
        }

        //
        // Common: Install/load AnnotSV annotations
        //
        if (params.annotsv_annotations) {
            annotations = Channel.fromPath(params.annotsv_annotations).map{ it -> [ [id:it.baseName], it ] }.collect()
        } else {
            ANNOTSV_INSTALLANNOTATIONS()
            ch_versions = ch_versions.mix(ANNOTSV_INSTALLANNOTATIONS.out.versions)
            annotations = ANNOTSV_INSTALLANNOTATIONS.out.annotations.map{ it -> [ [id:it.baseName], it ] }.collect()
        }

        //
        // Common: Prepare shared annotation channels
        //
        ch_gene_transcripts   = params.gene_transcripts   ? Channel.fromPath(params.gene_transcripts).map{ it -> [ [id:it.baseName], it ] }.collect()   : Channel.value([[:], []])
        ch_candidate_genes    = params.candidate_genes    ? Channel.fromPath(params.candidate_genes).map{ it -> [ [id:it.baseName], it ] }.collect()    : Channel.value([[:], []])
        ch_false_positive_snv = params.false_positive_snv ? Channel.fromPath(params.false_positive_snv).map{ it -> [ [id:it.baseName], it ] }.collect() : Channel.value([[:], []])

        //
        // wes: CNV calling with CNVS_CALLING subworkflow
        //
        if (params.ngs_type == 'wes') {

            ch_intervals_cnvs = params.intervals

            // Define the run name
            if (params.runname) { runname = params.runname }
            else { runname = new Date().format("yyyy-MM-dd_HH-mm") }
            println "Run name: $runname"

            bam_file_list = ch_bam_svs.map{ meta, bam, bai -> bam }.collect().map { files -> files.sort { it.name } }
            bai_file_list = ch_bam_svs.map{ meta, bam, bai -> bai }.collect().map { files -> files.sort { it.name } }

            // define the input channels
            samples2analyce = params.samples_cnvs ? Channel.fromPath(params.samples_cnvs, checkIfExists: true).collect() : Channel.value([])

            ch_small_variants = params.candidate_small_variants
                ? Channel.value(file(params.candidate_small_variants))
                : Channel.value(file('NO_FILE'))

            CNVS_CALLING(
                bam_file_list,
                bai_file_list,
                ch_intervals_cnvs,
                ch_fai,
                runname,
                samples2analyce,
                annotations,
                ch_small_variants,
                ch_gene_transcripts,
                ch_candidate_genes,
                ch_false_positive_snv,
                ch_glowgenes_ranking
            )
        //
        // WGS: Manta germline SV calling + AnnotSV annotation
        //
        } else if (params.ngs_type == 'wgs') {

            ch_manta_config = params.manta_config
                ? Channel.fromPath(params.manta_config, checkIfExists: true)
                : Channel.empty()

            SV_CALLING (
                ch_bam_svs,
                ch_fasta,
                ch_fai,
                ch_manta_config,
                annotations,
                ch_candidate_genes,
                ch_false_positive_snv,
                ch_gene_transcripts
            )
            ch_versions = ch_versions.mix(SV_CALLING.out.versions)

            SV_CALLING_DELLY (
                ch_bam_svs,
                ch_fasta,
                ch_fai,
                annotations,
                ch_candidate_genes,
                ch_false_positive_snv,
                ch_gene_transcripts
            )
            ch_versions = ch_versions.mix(SV_CALLING_DELLY.out.versions)
        }
    }

    // ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
    //  EXPANSION HUNTER
    // ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~

    // Run EXPANSIONHUNTER as an additional step
    if (params.run_expansionhunter) {
        // Get BAMs from mapping or from input samplesheet
        if (params.mapping) {
            ch_bam_expansionhunter = MAPPING.out.bam
        } else {
            ch_bam_expansionhunter = INPUT_CHECK.out.bams.map{ meta, bam, bai -> check_bam(meta, bam, bai) }
        }
        EXPANSIONHUNTER(
            ch_bam_expansionhunter,
            ch_fasta,
            ch_fai,
            ch_variant_catalog
        )
        ch_versions = ch_versions.mix(EXPANSIONHUNTER.out.versions)
    }

    // ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
    //  MOSDEPTH
    // ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~

    // Run MOSDEPTH as an additional step
    if (params.run_mosdepth) {
        // Get BAMs from mapping or from input samplesheet
        if (params.mapping) {
            ch_bam_mosdepth = MAPPING.out.bam
        } else {
            ch_bam_mosdepth = INPUT_CHECK.out.bams.map{ meta, bam, bai -> check_bam(meta, bam, bai) }
        }

        if (params.mosdepth_bed) {
            ch_bed_mosdepth = Channel.fromPath(params.mosdepth_bed)
            .map { bed -> [[id: bed.baseName], bed] }
            
            ch_mosdepth_input = ch_bam_mosdepth
            .combine(ch_bed_mosdepth)
            .map { meta, bam, bai, bed_meta, bed ->
                [meta, bam, bai, bed]
            }
        
        } else {
            ch_mosdepth_input = ch_bam_mosdepth.map { meta, bam, bai -> 
                [meta, bam, bai, []]
            }
        }

        MOSDEPTH(
            ch_mosdepth_input,
            ch_fasta
        )
        ch_versions = ch_versions.mix(MOSDEPTH.out.versions_mosdepth)

        if (params.mosdepth_mode == 'fast') {
        DECOMPRESS_MOSDEPTH_QUANTIZED(MOSDEPTH.out.quantized_bed)
        ch_versions = ch_versions.mix(DECOMPRESS_MOSDEPTH_QUANTIZED.out.versions)
        }

    }

    // ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
    //  SOFTWARE VERSIONS & MULTIQC
    // ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~

    CUSTOM_DUMPSOFTWAREVERSIONS (
        ch_versions.unique().collectFile(name: 'collated_versions.yml')
    )

    //
    // MODULE: MultiQC
    //
    workflow_summary    = WorkflowSnvs.paramsSummaryMultiqc(workflow, summary_params)
    ch_workflow_summary = Channel.value(workflow_summary)

    methods_description    = WorkflowSnvs.methodsDescriptionText(workflow, ch_multiqc_custom_methods_description, params)
    ch_methods_description = Channel.value(methods_description)

    ch_multiqc_files = Channel.empty()
    ch_multiqc_files = ch_multiqc_files.mix(ch_workflow_summary.collectFile(name: 'workflow_summary_mqc.yaml'))
    ch_multiqc_files = ch_multiqc_files.mix(ch_methods_description.collectFile(name: 'methods_description_mqc.yaml'))
    ch_multiqc_files = ch_multiqc_files.mix(CUSTOM_DUMPSOFTWAREVERSIONS.out.mqc_yml.collect())

    if (params.mapping) {
        ch_multiqc_files = ch_multiqc_files.mix(FASTQC.out.zip.collect{it[1]}.ifEmpty([]))
    }

    MULTIQC (
        ch_multiqc_files.collect(),
        ch_multiqc_config.toList(),
        ch_multiqc_custom_config.toList(),
        ch_multiqc_logo.toList()
    )
    multiqc_report = MULTIQC.out.report.toList()
}

/*
~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
    FUNCTIONS
~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
*/

def check_fastq (meta, reads) {
    if (reads.size() == 0) {
        exit 1, "ERROR: Please check input samplesheet -> No FastQ files provided for one or more samples!"
    } else {
        return [ meta, reads ]
    }
}

def check_bam (meta, bam, bai) {
    if (bam.size() == 0) {
        exit 1, "ERROR: Please check input samplesheet -> No bam files provided for one or more samples!"
    } else {
        return [ meta, bam, bai ]
    }
}

def check_vcf (meta, vcf, tbi) {
    if (vcf.size() == 0) {
        exit 1, "ERROR: Please check input samplesheet -> No vcf files provided for one or more samples!"
    } else {
        return [ meta, vcf, tbi ]
    }
}

/*
~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
    COMPLETION EMAIL AND SUMMARY
~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
*/

workflow.onComplete {
    if (params.email || params.email_on_fail) {
        NfcoreTemplate.email(workflow, params, summary_params, projectDir, log, multiqc_report)
    }
    NfcoreTemplate.dump_parameters(workflow, params)
    NfcoreTemplate.summary(workflow, params, log)
    if (params.hook_url) {
        NfcoreTemplate.IM_notification(workflow, params, summary_params, projectDir, log)
    }
}

workflow.onError {
    if (workflow.errorReport.contains("Process requirement exceeds available memory")) {
        println("🛑 Default resources exceed availability 🛑 ")
        println("💡 See here on how to configure pipeline: https://nf-co.re/docs/usage/configuration#tuning-workflow-resources 💡")
    }
}

/*
~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
    THE END
~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
*/
